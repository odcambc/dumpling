rule process_counts:
    """Runs script to process variant counts generated by GATK ASM into a table of counts per sample,
    as is required for running Enrich2 and Rosace."""
    input:
        expand("results/{{experiment}}/gatk/{samples}.variantCounts", samples=samples),
    output:
        expand("results/{{experiment}}/processed_counts/enrich_format/{samples}.tsv", samples=samples),
        expand("results/{{experiment}}/processed_counts/{samples}.csv", samples=samples),
        expand(
            "stats/{{experiment}}/processing/{samples}_total_processing.tsv",
            samples=samples,
        ),
    params:
        regenerate_variants=config["regenerate_variants"],
        samples=samples,
        gatk_dir="results/{experiment}/gatk/",
    benchmark:
        "benchmarks/{experiment}/{experiment}.process_counts.benchmark.txt"
    log:
        "logs/{experiment}/scripts/{experiment}.process_counts.log",
    script:
        "scripts/process_counts.py"

rule remove_zeros:
    """Runs script to remove unobserved variants from count files. This is only run
    if the remove_zeros parameter is set to True in the config file. This is used to
    handle some undesired behavior in Enrich2, and is not necessary for Rosace."""

    input:
        expand("results/{{experiment}}/processed_counts/enrich_format/{samples}.tsv", samples=samples),
    output:
        expand("results/{{experiment}}/processed_counts/removed_zeros/{samples}.tsv", samples=samples),
    params:
        regenerate_variants=config["regenerate_variants"],
        samples=samples,
    benchmark:
        "benchmarks/{experiment}/{experiment}.remove_zeros.benchmark.txt"
    log:
        "logs/{experiment}/scripts/{experiment}.remove_zeros.log",
    script:
        "scripts/remove_zeros.py"
